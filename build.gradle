// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.9.25'
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.4.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.karumi:shot:6.1.0'
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:0.14.0"
    }
}

plugins {
    id 'com.diffplug.spotless' version '6.25.0'
    id 'org.jetbrains.dokka' version '1.9.20'
    id "org.jetbrains.kotlinx.kover" version "0.8.0"
}

kover {
    merge {
        allProjects()
    }
}

apply plugin: 'binary-compatibility-validator'

apiValidation {
    ignoredProjects += ["sample-shared", "sample", "sample-groupie", "sample-epoxy"]
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        jcenter()
    }
}

subprojects {
    ext {
        artifactVersion = '0.6.0'
        ktlintVersion = '1.2.1'
    }

    apply plugin: 'com.diffplug.spotless'
    apply plugin: "org.jetbrains.kotlinx.kover"

    afterEvaluate { project ->
        if (!project.hasProperty('ignoreLint') || !project.ignoreLint) {
            spotless {
                kotlin {
                    target "**/*.kt"
                    ktlint(ktlintVersion).setEditorConfigPath("$rootDir/.editorconfig")
                    endWithNewline()
                    indentWithSpaces()
                    trimTrailingWhitespace()
                }

                format 'misc', {
                    target '**/*.gradle', '**/*.md', '**/.gitignore'
                    indentWithSpaces()
                    trimTrailingWhitespace()
                }

                format 'xml', {
                    target '**/*.xml'
                    indentWithSpaces()
                    trimTrailingWhitespace()
                }
            }
        }

        if (project.hasProperty('android')) {
            android {
                buildFeatures {
                    viewBinding true
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = "1.8"
                    // Adding this flag causes removing of Intrinsics.checkParameterIsNotNull()
                    // checks in generated Java bytecode, which eases our logic of
                    // retrieving loggable links to VH files
                    freeCompilerArgs += ["-Xno-param-assertions"]
                }

                testOptions {
                    animationsDisabled = true
                }
            }
        }
    }
}

apply plugin: 'android-reporting'
