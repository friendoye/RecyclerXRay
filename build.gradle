// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.10'
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.0-alpha13'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"
        classpath 'com.karumi:shot:4.4.0'
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:0.2.3"
        classpath "com.hiya:jacoco-android:0.2"
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.7.0'
}

apply plugin: 'binary-compatibility-validator'

apiValidation {
    ignoredProjects += ["sample-shared", "sample", "sample-groupie", "sample-epoxy"]
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        jcenter()
    }
}

subprojects {
    ext {
        artifactVersion = '0.6.0'
    }

    apply plugin: 'com.diffplug.spotless'

    afterEvaluate { project ->
        if (!project.hasProperty('ignoreLint') || !project.ignoreLint) {
            spotless {
                kotlin {
                    target "**/*.kt"
                    ktlint('0.35.0')
                    endWithNewline()
                    indentWithSpaces()
                    trimTrailingWhitespace()
                }

                format 'misc', {
                    target '**/*.gradle', '**/*.md', '**/.gitignore'
                    indentWithSpaces()
                    trimTrailingWhitespace()
                }

                format 'xml', {
                    target '**/*.xml'
                    indentWithSpaces()
                    trimTrailingWhitespace()
                }
            }
        }

        if (project.hasProperty('android')) {
            android {
                buildFeatures {
                    viewBinding true
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = "1.8"
                    // Adding this flag causes removing of Intrinsics.checkParameterIsNotNull()
                    // checks in generated Java bytecode, which eases our logic of
                    // retrieving loggable links to VH files
                    freeCompilerArgs += ["-Xno-param-assertions"]
                }

                testOptions {
                    animationsDisabled = true
                }
            }
        }
    }
}

apply plugin: 'android-reporting'
