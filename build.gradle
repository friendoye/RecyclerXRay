// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.18"
        classpath 'com.karumi:shot:4.4.0'
        classpath "com.gradle:build-scan-plugin:2.4.2"
        classpath "org.jetbrains.kotlinx:binary-compatibility-validator:0.2.3"
        classpath "com.dicedmelon.gradle:jacoco-android:0.1.4"
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.28.0'
}

apply plugin: 'binary-compatibility-validator'

apiValidation {
    ignoredProjects += ["sample"]
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://maven.google.com" }
        jcenter()
    }
}

subprojects {
    ext {
        artifactVersion = '0.5.0'
    }

    apply plugin: 'com.diffplug.gradle.spotless'

    afterEvaluate { project ->
        if (!project.hasProperty('ignoreLint') || !project.ignoreLint) {
            spotless {
                kotlin {
                    target "**/*.kt"
                    ktlint('0.35.0')
                    endWithNewline()
                    indentWithSpaces()
                    trimTrailingWhitespace()
                }

                format 'misc', {
                    target '**/*.gradle', '**/*.md', '**/.gitignore'
                    indentWithSpaces()
                    trimTrailingWhitespace()
                }

                format 'xml', {
                    target '**/*.xml'
                    indentWithSpaces()
                    trimTrailingWhitespace()
                }
            }
        }

        if (project.hasProperty('android')) {
            android {
                viewBinding {
                    enabled = true
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

                kotlinOptions {
                    jvmTarget = "1.8"
                    // Adding this flag causes removing of Intrinsics.checkParameterIsNotNull()
                    // checks in generated Java bytecode, which eases our logic of
                    // retrieving loggable links to VH files
                    freeCompilerArgs += ["-Xno-param-assertions"]
                }

                testOptions {
                    animationsDisabled = true
                }
            }
        }
    }
}

apply plugin: 'android-reporting'
apply plugin: 'com.gradle.build-scan'

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}