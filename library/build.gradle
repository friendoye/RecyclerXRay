apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'shot'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29

        testInstrumentationRunner "com.friendoye.recyclerxray.runner.RecyclerXRayTestRunner"
        testApplicationId "com.friendoye.recyclerxray.test"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.all {
            jacoco {
                // To accumulate coverage from Robolectric
                includeNoLocationClasses = true
            }
        }
    }

    lintOptions {
        textReport true
        textOutput 'stdout'

        checkReleaseBuilds false
        checkDependencies true
        ignoreTestSources true

        disable 'RtlEnabled'
    }

    kotlinOptions {
        allWarningsAsErrors = true
    }

    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }

    configurations.all {
        resolutionStrategy {
            force("androidx.lifecycle:lifecycle-common:2.2.0")
            force("androidx.collection:collection:1.1.0")
            force("androidx.lifecycle:lifecycle-runtime:2.0.0")
            force("androidx.arch.core:core-common:2.0.0")
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-alpha05'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    testImplementation "junit:junit:4.13"
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation "org.robolectric:robolectric:4.3.1"
    testImplementation 'androidx.test:core:1.3.0'

    androidTestImplementation "androidx.lifecycle:lifecycle-common:2.2.0"
    androidTestImplementation 'androidx.test:core-ktx:1.3.0'
    androidTestImplementation "androidx.test:runner:1.3.0"
    androidTestImplementation "androidx.test:rules:1.2.0"
    androidTestImplementation "androidx.test:core:1.2.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0'
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.ext:junit-ktx:1.1.1"
}

ext {
    bintrayName = 'recyclerxray'

    libraryName = 'RecyclerXRay'
    artifact = 'recyclerxray'
    libraryVersion = artifactVersion

    libraryDescription = 'Tool for visual debugging of complex RecyclerViews.'
}

apply from: '../deploy.gradle'